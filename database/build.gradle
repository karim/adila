plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.1'
}

group = 'com.karimeldeeb.adila'
version = '16.8.31'

compileJava {
    // Suppress warnings about using Java 5
    options.compilerArgs << "-Xlint:-options"

    // Compile with Java 5
    sourceCompatibility = 1.5
    targetCompatibility = 1.5

    // Location of Java 5 JDK runtime
    options.bootClasspath = "$System.env.JAVA5_HOME/jre/lib/rt.jar"
}

dependencies {
    compile project(':library')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = "$bintray_user"
    key = "$bintray_key"

    publications = ['maven']
    publish = true

    pkg {
        repo = 'adila'
        name = 'database'
        desc = 'Adila Database'
        licenses = ['MIT']

        websiteUrl = 'https://github.com/karim/adila'
        issueTrackerUrl = 'https://github.com/karim/adila/issues'
        vcsUrl = 'https://github.com/karim/adila.git'
        githubRepo = 'karim/adila'

        version { name = project.version }
    }
}
