plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.1'
}

group = 'com.karimeldeeb.adila'
version = '16.8.30'

compileJava {
    // Suppress warnings about using Java 5
    options.compilerArgs << "-Xlint:-options"

    // Compile with Java 5
    sourceCompatibility = 1.5
    targetCompatibility = 1.5

    // Location of Java 5 JDK runtime
    options.bootClasspath = "$System.env.JAVA5_HOME/jre/lib/rt.jar"
}

dependencies {
    // The included file is extracted from "Android SDK 1.0 r2", which you can downloaded from
    // http://developer.android.com/sdk/older_releases.html
    compile files('libs/android.jar')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = "$bintray_user"
    key = "$bintray_key"

    publications = ['maven']
    publish = true

    pkg {
        repo = 'adila'
        name = 'library'
        desc = 'Adila Library'
        licenses = ['MIT']

        websiteUrl = 'https://github.com/karim/adila'
        issueTrackerUrl = 'https://github.com/karim/adila/issues'
        vcsUrl = 'https://github.com/karim/adila.git'
        githubRepo = 'karim/adila'

        version { name = project.version }
    }
}
